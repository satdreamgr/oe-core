#@TYPE: Distribution
#@NAME: OpenPLi
#@DESCRIPTION: Embedded Linux Distribution Configuration for STB's

require reporefs.conf

DISTRO_FEATURES_BACKFILL_CONSIDERED += "pulseaudio"

DISTRO_NAME ?= "openpli"
DISTRO_HOST ?= "${@os.uname()[1]}"
DISTRO_VERSION ?= "homebuild"
DISTRO_TYPE ?= "release"

IPKG_VARIANT = "opkg"
PACKAGE_DEBUG_SPLIT_STYLE = "debug-without-src"

FEED_NAME ?= "${DISTRO_NAME}-${DISTRO_VERSION}"
DISTRO_FEED_URI ?= "http://${DISTRO_HOST}/feeds/${FEED_NAME}"
DISTRO_FEED_PREFIX ?= "${DISTRO_NAME}"

IMAGE_NAME = "${IMAGE_BASENAME}-${DISTRO_VERSION}-${MACHINE}"
IMAGE_LINK_NAME = ""
CACHE ?= "${TMPDIR}/cache/${LIBC}/${MACHINE}"
DEPLOY_DIR ?= "${TMPDIR}/deploy/${LIBC}"
DEPLOY_DIR_IMAGE = "${DEPLOY_DIR}/images/${MACHINE}"

PREFERRED_VERSION_ffmpeg = "4.%"
PREFERRED_VERSION_libupnp ?= "1.6.26"

PREFERRED_RPROVIDER_libfribidi = "libfribidi"
PREFERRED_RPROVIDER_libav = "ffmpeg"
PREFERRED_RPROVIDER_libid3tag = "libid3tag"
PREFERRED_RPROVIDER_libcrypto = "openssl"
PREFERRED_RPROVIDER_libssl = "openssl"
PREFERRED_RPROVIDER_libtool ?= "libtool"
PREFERRED_RPROVIDER_libtool-native ?= "libtool-native"
PREFERRED_RPROVIDER_libltdl ?= "libtool"

PREFERRED_RPROVIDER_${PYTHON_PN}-pycrypto ?= "python3-pycryptodome"
PREFERRED_RPROVIDER_${PYTHON_PN}-yenc ?= "python3-sabyenc3"

PREFERRED_RPROVIDER_ssh ?= "openssh"

PREFERRED_PROVIDER_virtual/enigma2-mediaservice ?= "enigma2-plugin-systemplugins-servicemp3"

PREFERRED_PROVIDERS += "virtual/${TARGET_PREFIX}gcc-intermediate:gcc-cross-intermediate"
PREFERRED_PROVIDERS += "virtual/${TARGET_PREFIX}gcc:gcc-cross"
PREFERRED_PROVIDERS += "virtual/${TARGET_PREFIX}g++:gcc-cross"

# Gstreamer 1.x
GST_VERSION = "1.0"

LICENSE_FLAGS_WHITELIST = "commercial"
COMMERCIAL_AUDIO_PLUGINS ?= "gst-plugins-ugly-mad gst-plugins-ugly-mpegaudioparse"
COMMERCIAL_VIDEO_PLUGINS ?= "gst-plugins-ugly-mpeg2dec gst-plugins-ugly-mpegstream gst-plugins-bad-mpegvideoparse"

# OE optimization defaults to -O2 which makes for much larger binaries.
# Overridden here to use -Os instead, resulting in smaller images.

FULL_OPTIMIZATION = "-Os -pipe ${DEBUG_FLAGS}"

# build some core libs with better compiler optimization for better performance

O2_OPT = "-O2 -pipe ${DEBUG_FLAGS}"
O3_OPT = "-O3 -pipe ${DEBUG_FLAGS}"

FULL_OPTIMIZATION_pn-flac = "${O2_OPT}"
FULL_OPTIMIZATION_pn-jpeg = "${O2_OPT}"
FULL_OPTIMIZATION_pn-lame = "${O2_OPT}"
FULL_OPTIMIZATION_pn-liba52 = "${O2_OPT}"
FULL_OPTIMIZATION_pn-libdca = "${O2_OPT}"
FULL_OPTIMIZATION_pn-libgcrypt = "${O2_OPT}"
FULL_OPTIMIZATION_pn-libmad = "${O2_OPT}"
FULL_OPTIMIZATION_pn-libogg = "${O2_OPT}"
FULL_OPTIMIZATION_pn-libpng = "${O2_OPT}"
FULL_OPTIMIZATION_pn-libsamplerate = "${O2_OPT}"
FULL_OPTIMIZATION_pn-libvorbis = "${O2_OPT}"
FULL_OPTIMIZATION_pn-tremor = "${O2_OPT}"
FULL_OPTIMIZATION_pn-zlib = "${O2_OPT}"

DISTRO_FEATURES ?= "alsa bluetooth directfb ext2 largefile wifi nfs zeroconf mplt pam ${DISTRO_FEATURES_LIBC} ipv4 ipv6"

QA_LOGFILE = "${TMPDIR}/qa.log"

OEINCLUDELOGS ?= "yes"
KERNEL_CONSOLE ?= "ttyS0"

BB_GENERATE_MIRROR_TARBALLS ??= "0"

NO32LIBS ??= "1"

# Missing checksums should raise an error
BB_STRICT_CHECKSUM = "0"

# Place output in "tmp" as before, not "tmp-glibc"
TCLIBCAPPEND = ""

# Don't grab feeds for lower level archs, e.g. mips32el should not
# attempt to fetch mipsel, and armv7 need not fetch armv5 etcetera.
DISTRO_FEED_ARCHS = "all ${TUNE_PKGARCH} ${MACHINE_ARCH}"

# Use OpenSSL only
PACKAGECONFIG_pn-curl = "${@bb.utils.contains("DISTRO_FEATURES", "ipv6", "ipv6", "", d)} ssl proxy zlib libssh2"
PACKAGECONFIG_pn-openldap = "openssl modules mdb ldap meta monitor null passwd shell proxycache dnssrv ${@bb.utils.contains('DISTRO_FEATURES', 'ipv6', 'ipv6', '', d)}"
PACKAGECONFIG_pn-neon = "expat openssl libproxy webdav zlib"

# Allow java, svn and unzip executed on host
HOSTTOOLS += "java svn unzip"
